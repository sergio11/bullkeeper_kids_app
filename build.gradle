apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

buildscript {

    ext.kotlin_version = '1.3.0'
    ext.gradle_tools = '3.1.3'
    ext.build_tools = '28.0.3'
    ext.realm_gradle = '5.7.0'

    ext.compile_sdk = 28
    ext.target_sdk = 28
    ext.min_sdk = 21

    ext.application_id = 'com.sanchez.sanchez.bullkeeper_kids'
    ext.version_name = '0.1.40'
    ext.version_code = 140
    ext.test_runner = 'android.support.test.runner.AndroidJUnitRunner'

    repositories {
        google()
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:3.3.2"
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.realm:realm-gradle-plugin:$realm_gradle"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven {
          url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
        }
    }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task runUnitTests(dependsOn: [':app:testDebugUnitTest']) {
  description 'Run all unit tests'
}

task runAcceptanceTests(dependsOn: [':app:connectedAndroidTest']) {
  description 'Run all acceptance tests.'
}

task deployDebug(type: Exec, dependsOn: 'app:installDebug') {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream {
      inputStream -> properties.load(inputStream)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    def adb = "$sdkDir/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.fernandocejas.sample/com.fernandocejas.sample.core.navigation.RouteActivity'
  }
}
